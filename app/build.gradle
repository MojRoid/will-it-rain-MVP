apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'kotlin-android'
apply from: 'dependencies.gradle'
apply from: 'config.gradle'
apply from: 'jacoco.gradle'
apply from: 'jgiven.gradle'

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0

android {
    compileSdkVersion compile_sdk_version
    buildToolsVersion project.build_tools_version

    defaultConfig {
        applicationId "moj.rain"
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        versionCode versionMajor * 10000000 + versionMinor * 100000 + versionPatch * 1000 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath false
            }
        }

        buildConfigField "String", "DARK_SKY_BASE_URL", "\"${config.commonProperties.dark_sky_base_url}\""
        buildConfigField "String", "GEOCODER_BASE_URL", "\"${config.commonProperties.geocoder_base_url}\""
        buildConfigField "String", "GOOGLE_STATIC_MAPS_FULL_URL", "\"${config.commonProperties.google_static_maps_full_url}\""

        buildConfigField "String", "DARK_SKY_API_KEY", "\"${config.keysProperties.dark_sky_api_key}\""
        buildConfigField "String", "GOOGLE_API_KEY", "\"${config.keysProperties.google_api_key}\""

        signingConfigs {
            release {
                keyAlias config.keystoreProperties.key_alias
                keyPassword config.keystoreProperties.key_password
                storeFile file(config.keystoreProperties.store_file)
                storePassword config.keystoreProperties.store_password
            }
        }

        buildTypes {
            debug {
                testCoverageEnabled true
                applicationIdSuffix ".debug"
                resValue "string", "app_name", config.getAppName("DEBUG")
            }
            release {
                minifyEnabled true
                useProguard true
                resValue "string", "app_name", config.getAppName("RELEASE")
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
                signingConfig signingConfigs.release
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}
